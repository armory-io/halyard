/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  ext {
    clouddriverVersion = "5.47.1"
    front50Version = "2.15.0"
    fiatVersion = "1.13.1"
  }
  repositories {
    jcenter()
    maven { url "https://spinnaker.bintray.com/gradle" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker.gradle:spinnaker-dev-plugin:6.5.0'
    if (Boolean.valueOf(enablePublishing)) {
      classpath "com.netflix.spinnaker.gradle:spinnaker-gradle-project:6.5.0"
    }
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
  }
}

allprojects {
  group = "com.netflix.spinnaker.halyard"
  apply plugin: 'spinnaker.base-project'
  apply plugin: 'java-library'
  apply plugin: 'groovy'

  test {
    testLogging {
      showStandardStreams = false
      exceptionFormat = 'full'
    }
    minHeapSize = "512m"
    maxHeapSize = "512m"
  }

  dependencies {
    implementation platform("com.netflix.spinnaker.kork:kork-bom:$korkVersion")

    annotationProcessor platform("com.netflix.spinnaker.kork:kork-bom:$korkVersion")
    annotationProcessor "org.projectlombok:lombok"
    testAnnotationProcessor platform("com.netflix.spinnaker.kork:kork-bom:$korkVersion")
    testAnnotationProcessor "org.projectlombok:lombok"
  }

  configurations.all {
    resolutionStrategy {
      force group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.+' // Remove CVE-2019-10173, upgrade xstream dependency
      force group: 'commons-codec', name: 'commons-codec', version: '1.+' // Remove CVE SONATYPE-2012-0050, upgrade commons-codec dependency
      force group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.+' // Remove CVE-2019-17267, CVE-2019-16335, CVE-2019-14540, CVE-2019-14540 and CVE-2019-17531 , upgrade jackson-databind dependency
      force group: 'com.hubspot.jinjava', name: 'jinjava', version: '2.5.+' // Remove CVE-2018-18893, upgrade jinjava dependency
      force group: 'org.testng', name: 'testng', version: '7.1.+' // Remove CVE SONATYPE-2019-0115, upgrade testng dependency
      force group: 'com.github.ben-manes.caff eine', name: 'caffeine', version: '2.8.+' // Remove SONATYPE-2019-0282, upgrade caffeine dependency
      force group: 'org.codehaus.plexus', name: 'plexus-utils', version: '3.3.+' // Remove CVEs SONATYPE-2015-0173 and SONATYPE-2016-0398, upgrade plexus-utils dependency
      force group: 'org.apache.commons', name: 'commons-compress', version: '1.+' // Remove CVE-2019-12402, upgrade commons-compress dependency
      force group: 'commons-collections', name: 'commons-collections', version:'3.2.+' // Remove CVE SONATYPE-2015-0002, upgrade commons-collections dependency
      force group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.1.+' // Remove CVE-2019-10219, upgrade hibernate-validator dependency
      force group: 'org.jboss.logging', name: 'jboss-logging', version: '3.4.+' // Remove CVE-2017-2595, upgrade jboss-logging
      force group: 'commons-net', name: 'commons-net', version: '3.+' // Remove apache issue NET-579, upgrade commons-net dependency
      force group: 'io.netty', name: 'netty-codec-http', version: '4.1.+' // Remove CVE-2019-16869, upgrade netty-codec dependency
      force group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.31' // Remove CVE-2020-1938, upgrade tomcat-embed-core dependency
      force group: 'org.codehaus.plexus', name: 'plexus-utils', version: '3.0.24'
      force group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.3'
      force group: 'org.apache.commons', name: 'commons-compress', version: '1.20' // Remove CVE-2019-12402, upgrade commons-compress dependency
      force group: 'org.springframework', name: 'spring-web', version: '5.2.3.RELEASE'
      force group: 'org.javassist', name: 'javassist', version: '3.19.0-GA'
      force group: 'commons-io', name: 'commons-io', version: '2.6'
    }
  }
}

defaultTasks ':halyard-web:run'

// Creates scripts for entry points
// Subproject must apply application plugin to be able to call this method.
def createScript(project, mainClass, name) {
  project.tasks.create(name: name, type: CreateStartScripts) {
    outputDir       = new File(project.buildDir, 'scripts')
    mainClassName   = mainClass
    applicationName = name
    classpath       = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtime
  }
  project.tasks[name].dependsOn(project.jar)

  project.applicationDistribution.with {
    into("bin") {
      from(project.tasks[name])
      fileMode = 0755
    }
  }
}

subprojects { project ->
  if ([korkVersion, fiatVersion, clouddriverVersion, front50Version].any { it.endsWith("-SNAPSHOT") }) {
    logger.info("Enabling mavenLocal")
    repositories {
      mavenLocal()
    }
  }
}
