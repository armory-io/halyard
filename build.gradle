/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'io.spinnaker.project' version "$spinnakerGradleVersion" apply false
  id 'com.google.protobuf' version "0.8.12" apply false
  id "com.jfrog.artifactory" version "4.13.0"
}

ext {
  buildVersion = calculateVersion()
}

subprojects {
  group = "com.netflix.spinnaker.halyard"
  apply plugin: 'io.spinnaker.project'
  apply plugin: 'java-library'
  apply plugin: 'groovy'
  apply plugin: 'com.jfrog.artifactory'
  apply plugin: 'maven-publish'

  test {
    testLogging {
      showStandardStreams = false
      exceptionFormat = 'full'
    }
    minHeapSize = "512m"
    maxHeapSize = "512m"
  }

  if ([korkVersion, fiatVersion, clouddriverVersion, front50Version].any { it.endsWith("-SNAPSHOT") }) {
    logger.info("Enabling mavenLocal")
    repositories {
      mavenLocal()
    }
  }

  dependencies {
    implementation platform("com.netflix.spinnaker.kork:kork-bom:$korkVersion")

    annotationProcessor platform("com.netflix.spinnaker.kork:kork-bom:$korkVersion")
    annotationProcessor "org.projectlombok:lombok"
    testAnnotationProcessor platform("com.netflix.spinnaker.kork:kork-bom:$korkVersion")
    testAnnotationProcessor "org.projectlombok:lombok"
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }

  artifactory {
    contextUrl = "https://armory.jfrog.io/armory"
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
      repository {
        repoKey = "gradle-dev"
        username = "${artifactory_user}"
        password = "${artifactory_password}"
        maven = true
      }
      defaults {
        publications('mavenJava')
        publishArtifacts = true
        publishPom = true
      }
    }
  }

  task setVersion() {
    logger.info "Setting project version to: ${buildVersion}"
    project.setVersion(buildVersion)
  }
}

task version() {
  exec {
    workingDir projectDir
    commandLine 'mkdir', '-p', 'build'
  }
  def so = new FileOutputStream("build/version")
  exec {
    workingDir projectDir
    commandLine 'echo', "${version.toString().trim()}"
    standardOutput = so
  }
  println version
}

defaultTasks ':halyard-web:run'

// Creates scripts for entry points
// Subproject must apply application plugin to be able to call this method.
def createScript(project, mainClass, name) {
  project.tasks.create(name: name, type: CreateStartScripts) {
    outputDir       = new File(project.buildDir, 'scripts')
    mainClassName   = mainClass
    applicationName = name
    classpath       = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtime
  }
  project.tasks[name].dependsOn(project.jar)

  project.applicationDistribution.with {
    into("bin") {
      from(project.tasks[name])
      fileMode = 0755
    }
  }
}

def calculateVersion() {
  if (version != 'unspecified') {
    logger.info "Version already present: ${version}"
    return version
  }
  // get git hash
  def gitHash = new ByteArrayOutputStream().withStream { soHash ->
    exec {
      workingDir projectDir
      commandLine 'git', 'rev-parse', '--short', 'HEAD'
      standardOutput = soHash
    }
    return soHash.toString().trim()
  }.toString()
  // determine if the re are uncommitted changes
  def gitStatus = new ByteArrayOutputStream().withStream { soStatus ->
    exec {
      workingDir projectDir
      commandLine 'git', 'status', '--porcelain'
      standardOutput = soStatus
    }
    return soStatus.toString().trim()
  }.toString()

  def newVersion = "operator-${gitHash}"
  if (gitStatus != '') {
    newVersion += '-uncommitted'
  }
  logger.info "Calculated version: ${newVersion}"
  project.setVersion(newVersion)
  return newVersion
}
